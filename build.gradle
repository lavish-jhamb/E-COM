plugins {
	id 'java'
	id 'org.springframework.boot' version '3.4.3' apply false
	id 'io.spring.dependency-management' version '1.1.7'
}

repositories {
	mavenCentral()
}

subprojects {
	apply plugin: 'java'
	apply plugin: 'pmd'
	apply plugin: 'checkstyle'
	apply plugin: 'org.springframework.boot'
	apply plugin: 'io.spring.dependency-management'

	group = 'com.ecomhub'
	version = '0.0.1-SNAPSHOT'

	java {
		toolchain {
			languageVersion = JavaLanguageVersion.of(21)
		}
	}

	repositories {
		mavenCentral()
	}

	dependencies {
		implementation 'org.springframework.boot:spring-boot-starter-web'
		testImplementation 'org.springframework.boot:spring-boot-starter-test'
		testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	}

	tasks.named('test') {
		useJUnitPlatform()
	}

	pmd {
		toolVersion = '7.11.0'  // Specify PMD version
		ignoreFailures = true  // false - Fail build if issues are found
		consoleOutput = true    // Show results in console
		ruleSets = [] 		 // Disable default rules
		ruleSetFiles = files("$rootDir/config/pmd/ruleset.xml") // Custom rules
		reportsDir = file("$project.rootDir/build/reports/pmd/$project.name") // Report directory
	}

	checkstyle {
		toolVersion = "10.12.4"
		ignoreFailures = true  // false - Fail build if issues are found
		showViolations = true // Show violations in console
		configFile = "$project.rootDir/config/checkstyle/checkstyle.xml" as File // Checkstyle configuration
		reportsDir = file("$project.rootDir/build/reports/checkstyle/$project.name") // Report directory
	}

}
